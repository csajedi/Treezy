(* A two-part timekeeper contract containing... *)
(*  - A Merkle Root store for A remote calendar server *)
(*  - A validated store of the latest NIST randomness beacon (future) *)

(* @note: Owner cannot be changed, but delegated responsibilities are updatable. *)
(* Responsibilities: Chronos (randomness oracle) and Kairos (timestamp calendar anchor) *)
scilla_version 0


(***************************************************)
(*               Associated library                *)
(***************************************************)
library TimeStore

let one_msg =
  fun (msg: Message) =>
  let nil_msg = Nil {Message} in
  Cons {Message} msg nil_msg

(* error codes library *)
let code_success = Uint32 0
let code_not_authorized = Uint32 2
let code_invalid_params = Uint32 3

(***************************************************)
(*             The contract definition             *)
(***************************************************)
contract TimeStore
(owner: ByStr20,
chronos: ByStr20,
kairos: ByStr20)

(*Store of Data Timestamp's merkle root*)
field latestMerkleRoot: String = ""

(*Store of NIST's latest randomness*)
field latestNISTRandom: String = ""


(* @notice: Allows a `_sender` to update the merkle root from a remote Calendar server *)
(* @dev   : Access controls are simply if _sender is the set server key *)

transition updateRoot(new_root: String)
   
    (* Access Control  *)
    permitted = builtin eq _sender kairos;
    
    match permitted with
    | False =>
        (* the attempt to update root was rejected *)
        msg = {_tag: ""; 
                _recipient: _sender; 
                _amount: Uint128 0; 
                code: code_not_authorized};
        msgs = one_msg msg;
        send msgs
    | True =>
        (* An updated merkle root can be used to upgrade a proof transparently - there is no need to log past roots *)
        latestMerkleRoot := new_root;

        msg = {_tag: ""; 
                _recipient: _sender; 
                _amount: Uint128 0; 
                code: code_success};
        msgs = one_msg msg;
        send msgs
    end


end




(* @notice: Allows a `_sender` to update the NIST randomness string *)
(* @dev   : Access controls are simply if _sender is the authorized server key *)

transition updateRandom(new_random: String)
   
    (* Access Control  *)
    permitted = builtin eq _sender chronos;
    match permitted with
    | False =>
        (* the attempt to update the random beacon was rejected *)
        msg = {_tag: ""; 
                _recipient: _sender; 
                _amount: Uint128 0; 
                code: code_not_authorized};
        msgs = one_msg msg;
        send msgs
    | True =>
        (* Update the randomness beacon *)
        (* FUTURE: Require proof from calendar service for trustable oracle *)
        latestNISTRandom := new_random;

        msg = {_tag: ""; 
                _recipient: _sender; 
                _amount: Uint128 0; 
                code: code_success};
        msgs = one_msg msg;
        send msgs
    end


end


